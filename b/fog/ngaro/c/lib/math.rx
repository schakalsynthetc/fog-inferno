( Math Extensions ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~ )

chain: math'
{{
  : closer ( sg-sgn ) 2over / over - 2 / ;
  : loop   ( sg-sr  ) repeat closer 0; + again ;
---reveal---
  : squareRoot ( s-r ) 1 loop nip ;
}}

: gcd ( ab-n ) [ tuck mod dup ] while drop ;
: lcm ( ab-n ) 2over gcd [ * ] dip / ;
;chain

doc{
Overview
========
This library provides support for additional mathmatic operations not provided
by the core Retro language.

Functions
=========
+------------+-------+-----------------------------------------------------+
| Function   | Stack | Description                                         |
+============+=======+=====================================================+
| squareRoot | x-n   | Find an (approximate) square root for a given value |
+------------+-------+-----------------------------------------------------+
| gcd        | xy-n  | Find the greatest common denominator for two values |
+------------+-------+-----------------------------------------------------+
| lcm        | xy-n  | Find the least common multiplier for two values     |
+------------+-------+-----------------------------------------------------+
}doc

